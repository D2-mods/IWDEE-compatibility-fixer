/*
Check base classes (fix if abilities missing)
- this file does nothing if the original files are unmodded
*/


//Paladin
COPY_EXISTING ~CLABPA01.2DA~  ~override~
  COUNT_REGEXP_INSTANCES ~GA_SPCL213~  bg2protect   // check BG2 abilities
  COUNT_REGEXP_INSTANCES ~GA_SPCL212~  bg2detect

  PATCH_IF (bg2protect > 1) BEGIN
    REPLACE_TEXTUALLY ~GA_SPCL213~ ~****~
  END
  PATCH_IF (bg2detect > 1) BEGIN
    REPLACE_TEXTUALLY ~GA_SPCL212~ ~****~
  END

  COUNT_REGEXP_INSTANCES ~GA_SPCL211~   layonhands  // check IWD abilities
  COUNT_REGEXP_INSTANCES ~GA_SPCL212~   iwdprotect
  COUNT_REGEXP_INSTANCES ~GA_SPCL109~   iwddetect
  COUNT_REGEXP_INSTANCES ~AP_#PALDISE~  paldise
  COUNT_REGEXP_INSTANCES ~AP_#PALFEAR~  palfear
  COUNT_REGEXP_INSTANCES ~GA_SPCL111~   curedisease

  PATCH_IF (iwdprotect = 0) BEGIN
    LPF d2_kit_ability_adder STR_VAR resource=~SPCL212~ END
  END
  PATCH_IF (iwddetect = 0) BEGIN
    LPF d2_kit_ability_adder STR_VAR resource=~SPCL109~ END
  END
  PATCH_IF (paldise = 0) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~#PALDISE~ END
  END
  PATCH_IF (palfear = 0) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~#PALFEAR~ END
  END
  PATCH_IF (curedisease = 0) BEGIN
    PATCH_IF (layonhands > 0) BEGIN
      LPF d2_kit_ability_adder STR_VAR resource=~SPCL111~ END
    END
  END
BUT_ONLY UNLESS ~AP_C0PAL01~    // not if revised Paladin (Artisan's Kitpack)


//Ranger: Tracking
COPY_EXISTING ~CLABRN01.2DA~  ~override~
  COUNT_REGEXP_INSTANCES ~GA_SPCL922~  tracking
  PATCH_IF (tracking > 1) BEGIN
    REPLACE_TEXTUALLY ~GA_SPCL922~ ~****~
  END

  COUNT_REGEXP_INSTANCES ~GA_SPCL922~  tracking
  PATCH_IF (tracking = 0) BEGIN
    LPF d2_kit_ability_adder STR_VAR resource=~SPCL922~ END
  END
BUT_ONLY


//Thief: Evasion
COPY_EXISTING ~CLABTH01.2DA~  ~override~
  COUNT_REGEXP_INSTANCES ~AP_#EVASION~  evasion
  PATCH_IF (evasion > 1) BEGIN
    REPLACE_TEXTUALLY ~AP_#EVASION~ ~****~
  END

  COUNT_REGEXP_INSTANCES ~AP_#EVASION~  evasion
  PATCH_IF (evasion = 0) BEGIN
    LPF d2_kit_ability_adder INT_VAR level=7 passive=1 STR_VAR resource=~#EVASION~ END
  END
BUT_ONLY


//Druid: poison immunity, timeless body, shapeshifts (if BG2 shapeshifts detected)
COPY_EXISTING ~CLABDR01.2DA~  ~override~
  COUNT_REGEXP_INSTANCES ~AP_SPCL939~  timeless
  COUNT_REGEXP_INSTANCES ~AP_SPCL925~  bg2resists
  COUNT_REGEXP_INSTANCES ~GA_SPCL611~  bg2shapeshifts

  PATCH_IF (timeless = 0) BEGIN
    LPF d2_kit_ability_adder INT_VAR level=15 passive=1 STR_VAR resource=~SPCL939~ END
  END

  PATCH_IF (bg2resists > 0) BEGIN
    REPLACE_TEXTUALLY ~AP_SPCL924~ ~****~
    REPLACE_TEXTUALLY ~AP_SPCL925~ ~****~
    REPLACE_TEXTUALLY ~AP_SPCL926~ ~****~
    REPLACE_TEXTUALLY ~AP_SPCL927~ ~****~
  END

  COUNT_REGEXP_INSTANCES ~AP_SPCL924~  poisonresist
  PATCH_IF (poisonresist = 0) BEGIN
    LPF d2_kit_ability_adder INT_VAR level=9 passive=1 STR_VAR resource=~SPCL924~ END
  END

  PATCH_IF (bg2shapeshifts > 0) BEGIN
    REPLACE_TEXTUALLY ~GA_SPCL611~ ~****~
    REPLACE_TEXTUALLY ~GA_SPCL612~ ~****~
    REPLACE_TEXTUALLY ~GA_SPCL613~ ~****~
    REPLACE_TEXTUALLY ~GA_SPCL107~ ~****~
    REPLACE_TEXTUALLY ~GA_SPCL108~ ~****~
    REPLACE_TEXTUALLY ~GA_SPCL101~ ~****~
    REPLACE_TEXTUALLY ~GA_SPCL112~ ~****~
    REPLACE_TEXTUALLY ~GA_SPCL113~ ~****~
    REPLACE_TEXTUALLY ~GA_SPCL114~ ~****~
    LPF d2_kit_ability_adder INT_VAR level=5 STR_VAR resource=~SPCL107~ END
    LPF d2_kit_ability_adder INT_VAR level=7 STR_VAR resource=~SPCL108~ END
    LPF d2_kit_ability_adder INT_VAR level=9 STR_VAR resource=~SPCL101~ END
    LPF d2_kit_ability_adder INT_VAR level=11 STR_VAR resource=~SPCL112~ END
    LPF d2_kit_ability_adder INT_VAR level=13 STR_VAR resource=~SPCL113~ END
    LPF d2_kit_ability_adder INT_VAR level=15 STR_VAR resource=~SPCL114~ END
  END
BUT_ONLY



/*
*/